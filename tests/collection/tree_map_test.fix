module TreeMapTest;

import Random;
import Minilib.Collection.TreeMap;
import Minilib.Collection.Trait.Map;
import Minilib.Collection.Trait.SortedMap;
import Minilib.Text.StringEx;
import Minilib.Testing.UnitTest;
import Minilib.Testing.TestUtilArray;
import Minilib.Trait.Traversable;

import MapTests;
import SortedMapTests;

test_upsert: TestCase;
test_upsert = (
    make_test("test_upsert") $ |_|
    let map = TreeMap::make();
    let map = map.upsert(1, [11], push_back(12));
    let map = map.upsert(2, [21], push_back(22));
    let map = map.upsert(3, [31], push_back(32));
    assert_equal("size", 3, map.get_size);;
    let map = map.upsert(1, [13], push_back(14));
    let map = map.upsert(2, [23], pop_back);
    assert_equal("size", 3, map.get_size);;
    assert_equal("eq", none(), map.find(0));;
    assert_equal("eq", some([11, 14]), map.find(1));;
    assert_equal("eq", some([]), map.find(2));;
    assert_equal("eq", some([31]), map.find(3));;
    pure()
);

test_find_range: TestCase;
test_find_range = (
    let testname = "test_find_range";
    make_test(testname) $ |_|
    let keys_sorted = Iterator::range(0, 30).to_array;
    let keys_shuffled = keys_sorted.reorder(shuffle(345));
    let map = keys_shuffled.to_iter.fold(
        TreeMap::make(), |k, map|
        map.insert(k, k*10)
    );
    let test_find_range_ok = |begin, end, expected| (
        let actual = map.find_range(begin, end).to_array;
        assert_equal("begin="+begin.to_string+" end="+end.to_string, expected, actual)
    );
    let test_find_raw_range_ok = |lt_begin, lt_end, expected| (
        let actual = map.find_raw_range(lt_begin, lt_end).to_array;
        assert_equal("raw_range", expected, actual)
    );

    test_find_range_ok(3, 6, [(3,30),(4,40),(5,50)]);;
    test_find_range_ok(5, 6, [(5,50)]);;
    test_find_range_ok(5, 5, []);;
    test_find_range_ok(-1, 1, [(0,0)]);;
    test_find_range_ok(29, 31, [(29,290)]);;
    test_find_raw_range_ok(|(k,_)| k < 11, |(k,_)| k < 14, [(11,110), (12,120), (13,130)]);;
    test_find_raw_range_ok(|(k,_)| k < 28, |(k,_)| true, [(28,280), (29,290)]);;
    test_find_raw_range_ok(|(k,_)| false, |(k,_)| k < 2, [(0,0), (1,10)]);;
    pure()
);

main: IO ();
main = (
    let empty_map: TreeMap I64 String = TreeMap::make();
     [
        MapTests::map_tests(empty_map),
        SortedMapTests::sorted_map_tests(empty_map),
        test_upsert,
        test_find_range,
        TestCase::empty
    ]
    .run_test_driver
);

