// Tests for Minilib.Collection.Trait::SortedMap
module SortedMapTests;

import Random;
import Minilib.Collection.Trait;
import Minilib.Testing.UnitTest;
import Minilib.Testing.TestUtilArray;
import Minilib.Trait.Traversable;

test_select_range:
    [map: SortedMap, MapKey map = I64, MapValue map = String, Map::MapIterator map = iter, iter: Iterator, Item iter = (I64, String)]
    map -> TestCase;
test_select_range = |empty_map| (
    let keys_sorted = Iterator::range(0, 30).to_array;
    let keyvalues_sorted = keys_sorted.Functor::map(|k| (k, (k*10).to_string));
    let keyvalues_shuffled = keyvalues_sorted.reorder(shuffle(345));
    let map = keyvalues_shuffled.to_iter.fold(empty_map, insert);
    make_table_test("test_select_range",
        [
            (included(3), excluded(6), [(3,"30"),(4,"40"),(5,"50")]),
            (excluded(3), included(6), [(4,"40"),(5,"50"),(6,"60")]),
            (included(5), excluded(6), [(5,"50")]),
            (included(5), included(5), [(5,"50")]),
            (included(5), excluded(5), []),
            (included(-1), excluded(1), [(0,"0")]),
            (excluded(28), excluded(31), [(29,"290")]),
            (excluded(10), excluded(14), [(11,"110"), (12,"120"), (13,"130")]),
            (included(28), unbound(), [(28,"280"), (29,"290")]),
            (unbound(), excluded(2), [(0,"0"), (1,"10")]),
            (unbound(), unbound(), keyvalues_sorted),
        ],
        |(begin, end, expected)|
        let actual = map.select_range(begin, end, true).to_array;
        assert_equal("ascending", expected, actual);;
        let actual = map.select_range(begin, end, false).to_array;
        assert_equal("decending", expected.reverse, actual);;
        pure()
    )
);

test_select_range_random:
    [map: SortedMap, MapKey map = I64, MapValue map = String, Map::MapIterator map = iter, iter: Iterator, Item iter = (I64, String)]
    map -> TestCase;
test_select_range_random = |empty_map| (
    make_table_test("test_select_range_random",
        [
            (32347, 100),
            (32348, 200),
        ],
        |(seed, n)|
        let random = Random::init_by_seed(seed.to_U64);
        let (random, keys) = random.generate_array(n, generate_U64 >> Functor::map(to_I64));
        let keyvalues = keys.map(|k| (k, k.to_string));
        let map = keyvalues.to_iter.fold(empty_map, insert);
        let keyvalues_sorted: Array (I64, String) = keyvalues.sort.dedup;
        range(0, 100).fold_m(
            random, |_, random|
            let (random, lb_type) = random.generate_U64.Functor::map(to_I64);
            let (random, lower_bound) = random.generate_U64.Functor::map(to_I64);
            let (random, ub_type) = random.generate_U64.Functor::map(to_I64);
            let (random, upper_bound) = random.generate_U64.Functor::map(to_I64);
            let make_bound = |type_, bound| if type_ % 3 == 0 { unbound() }
            else if type_ % 3 == 1 { included(bound) }
            else { excluded(bound) };
            let lower_bound = make_bound(lb_type, lower_bound);
            let upper_bound = make_bound(lb_type, upper_bound);
            let in_range: (I64, String) -> Bool = |(k, v)| (
                let lb_ok = match lower_bound {
                    unbound() => true,
                    included(bound) => bound <= k,
                    excluded(bound) => bound < k,
                };
                let ub_ok = match upper_bound {
                    unbound() => true,
                    included(bound) => k <= bound,
                    excluded(bound) => k < bound,
                };
                lb_ok && ub_ok
            );
            let expected: Array (I64, String) = keyvalues_sorted.to_iter.filter(in_range).to_array;
            let actual: Array (I64, String) = map.select_range(lower_bound, upper_bound, true).to_array;
            //eval debug_eprintln("(lower_bound, upper_bound, expected)=" + (lower_bound, upper_bound, expected).to_string);
            assert_equal("eq", expected, actual);;
            pure $ random
        ).forget
    )
);

sorted_map_tests:
    [map: SortedMap, MapKey map = I64, MapValue map = String, Map::MapIterator map = iter, iter: Iterator, Item iter = (I64, String)]
    map -> TestCase;
sorted_map_tests = |empty_map| (
    [
        test_select_range(empty_map),
        test_select_range_random(empty_map),
    ]
    .run_tests
);
