// `TreapSet` is a set that manages elements in sorted order.
//
// A treap is a randomized binary search tree.
// The time complexity of a single search, insertion, or deletion is `O(log n)` with high probability.
//
// `TreapSet` implements the `Set` and `SortedSet` trait.
//  If you import this module, you should also import `Minilib.Collection.Trait`.
//
// `TreapSet` manages elements as a node array.
// If you modify the `TreapSet`, make sure it is unique, otherwise the entire node array will be copied.
//
// ## Benchmark Result
// ```
// test_perf (n=1000000)
//  insert: 1.365991 seconds
//  to_iter.to_array: 0.176191 seconds
//  to_array: 0.216342 seconds
//  erase: 1.040427 seconds
// ```
module Minilib.Collection.TreapSet;

import Minilib.Common.Common;
import Minilib.Collection.TreapMap;
import Minilib.Collection.Trait;
import Minilib.Text.StringEx;

// The type of TreapSet.
type TreapSet kc k = box struct {
    map: TreapMap kc k ()
};

namespace TreapSet {
    // An empty TreapSet.
    empty: [kc: KeyCompare, Key kc = k] TreapSet kc k;
    empty = (
        TreapSet::make(KeyCompare::empty)
    );

    // Makes an empty TreapSet.
    //
    // # Parameters
    // # - `kc`: a key comparator
    make: [kc: KeyCompare, Key kc = k] kc -> TreapSet kc k;
    make = |kc| TreapSet {
        map: TreapMap::make(kc)
    };
}

// Implementation of the `Set` trait.
impl [kc: KeyCompare, Key kc = k] TreapSet kc k: Set {
    type SetElem (TreapSet kc k) = k;
    type SetIterator (TreapSet kc k) = Std::Iterator::MapIterator (TreapMapIterator kc k ()) (k, ()) k;

    insert = |k, set| set.mod_map(insert((k,())));
    erase = |k, set| set.mod_map(erase(k));
    is_empty = |set| set.@map.is_empty;
    get_size = |set| set.@map.get_size;
    contains = |k, set| set.@map.contains_key(k);
    to_iter = |set| set.@map.to_iter.Iterator::map(|(k,_)| k);
    to_array = |set| set.@map.to_array.Functor::map(|(k,_)| k);
    //from_iter = TreapSet::_from_iter;
}

// Implementation of the `SortedSetIF` trait.
impl [kc: KeyCompare, Key kc = k] TreapSet kc k: SortedSetIF {
    select_range = |lower_bound, upper_bound, ascending, set| (
        set.@map.select_range(lower_bound, upper_bound, ascending).Iterator::map(|(k,_)| k)
    );
}
