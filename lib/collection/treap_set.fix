// TreapSet is a set that manages keys in sorted order.
//
// A treap is a type of random binary search tree.
// Searches, insertions, and deletions have high probability of time complexity `O(log n)`.
//
// `TreapSet` implements the `Set` and `SortedSet` trait, so if you use `TreapSet`,
// you may also need to import `Minilib.Collection.Trait.Set` and `Minilib.Collection.Trait.SortedSet`.
module Minilib.Collection.TreapSet;

import Minilib.Common.Common;
import Minilib.Collection.TreapMap;
import Minilib.Collection.Trait.Set;
import Minilib.Collection.Trait.SortedSet;
import Minilib.Collection.Trait.Map;
import Minilib.Collection.Trait.SortedMap;
import Minilib.Text.StringEx;

// The type of TreapSet.
type TreapSet kc k = box struct {
    map: TreapMap kc k ()
};

namespace TreapSet {
    // An empty TreapSet.
    empty: [kc: KeyCompare, Key kc = k] TreapSet kc k;
    empty = (
        TreapSet::make(KeyCompare::empty)
    );

    // Makes an empty TreapSet.
    //
    // # Parameters
    // # - `kc`: a key comparator
    make: [kc: KeyCompare, Key kc = k] kc -> TreapSet kc k;
    make = |kc| TreapSet {
        map: TreapMap::make(kc)
    };
}

// Implementation of the `Set` trait.
impl [kc: KeyCompare, Key kc = k] TreapSet kc k: Set {
    type SetElem (TreapSet kc k) = k;
    type SetIterator (TreapSet kc k) = Std::Iterator::MapIterator (TreapMapIterator kc k ()) (k, ()) k;

    insert = |k, set| set.mod_map(insert(k,()));
    erase = |k, set| set.mod_map(erase(k));
    is_empty = |set| set.@map.is_empty;
    get_size = |set| set.@map.get_size;
    contains = |k, set| set.@map.contains_key(k);
    to_iter = |set| set.@map.to_iter.Iterator::map(|(k,_)| k);
    to_array = |set| set.@map.to_array.Functor::map(|(k,_)| k);
    //from_iter = TreapSet::_from_iter;
}

// Implementation of the `SortedSetIF` trait.
impl [kc: KeyCompare, Key kc = k] TreapSet kc k: SortedSetIF {
    select_range = |begin, end, ascending, set| (
        set.@map.select_range(begin, end, ascending).Iterator::map(|(k,_)| k)
    );
}
