// `PTreeSet` is a set that manages keys in sorted order.
//
// The elements of a `PTreeSet` are `I64`, or `U64`. It's limited, but fast.
//
// `PTreeSet` implements the `Set` and `SortedSet` trait, so if you import this module,
// you should also import `Minilib.Collection.Trait.Set` and `Minilib.Collection.Trait.SortedSet`.
//
// `PTreeSet` is built upon [a Patricia Tree](https://en.wikipedia.org/wiki/Radix_tree).
// In particular, I implemented the Patricia Tree with reference to the following paper:
// [Chris Okasaki and Andy Gill, "Fast Mergeable Integer Maps"](https://web.archive.org/web/20150417234429/https://ittc.ku.edu/~andygill/papers/IntMap98.pdf).
//
module Minilib.Collection.PTreeSet;

import Minilib.Collection.Internal.PatTree;
import Minilib.Collection.Trait.Set;
import Minilib.Collection.Trait.SortedSet;
import Minilib.Collection.Trait.Map;    // PatTree is a Map
import Minilib.Collection.Trait.SortedMap;
import Minilib.Text.StringEx;

// The trait of elements of a PTreeSet.
trait k : PTreeSetKey {
    // Converts from `k` to an internal key.
    _to_pattree_key: k -> PatTree::Key;
    // Converts from an internal key to `k`.
    _from_pattree_key: PatTree::Key -> k;
}

namespace PTreeSetKey {
    // Converts from `(PatTree::Key, v)` to `k`.
    _from_pattree_kv: [k : PTreeSetKey] (PatTree::Key, v) -> k;
    _from_pattree_kv = |(key, v)| key._from_pattree_key;
}

// An offset from I64 to U64.
_pattree_key_offset: U64;
_pattree_key_offset = 1_U64.shift_left(63_U64);
//_pattree_key_offset = 0_U64;     // for debug

impl I64: PTreeSetKey {
    _to_pattree_key = |i64| i64.to_U64 + _pattree_key_offset;
    _from_pattree_key = |u64| (u64 - _pattree_key_offset).to_I64;
}

impl U64: PTreeSetKey {
    _to_pattree_key = |u64| u64;
    _from_pattree_key = |u64| u64;
}

// The type of Patricia Tree Sets.
type PTreeSet k = unbox struct {
    // The internal Patricia Tree.
    tree: PTree (),
};

// The type of Patricia Tree Sets whose key is `I64`.
type PTreeSetI64 = PTreeSet I64;

// The type of Patricia Tree Sets whose key is `U64`.
type PTreeSetU64 = PTreeSet U64;

// An empty PTreeSet.
empty: [k: PTreeSetKey] PTreeSet k;
empty = PTreeSet { tree: empty };

impl [k: PTreeSetKey] PTreeSet k: Set {
    type SetElem (PTreeSet k) = k;
    type SetIterator (PTreeSet k) = Std::Iterator::MapIterator (PTreeIterator ()) (PatTree::Key, ()) k;

    is_empty = |set| set.@tree.is_empty;
    get_size = |set| set.@tree.get_size;
    contains = |k, set| set.@tree.contains_key(k._to_pattree_key);
    insert = |k, set| set.mod_tree(insert(k._to_pattree_key, ()));
    erase = |k, set| set.mod_tree(erase(k._to_pattree_key));
    to_iter = |set| set.@tree.to_iter.Iterator::map(_from_pattree_kv);
    to_array = |set| set.@tree.to_array.Functor::map(_from_pattree_kv);
    //from_iter = PTree::_from_iter;
}

impl [k: PTreeSetKey] PTreeSet k: SortedSetIF {
    select_range = |begin, end, ascending, set| (
        let begin = begin.Functor::map(_to_pattree_key);
        let end = end.Functor::map(_to_pattree_key);
        set.@tree.select_range(begin, end, ascending)
        .Iterator::map(_from_pattree_kv)
    );
}
